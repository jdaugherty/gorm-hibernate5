ext {
    datastoreGithubBranch = "8.0.x"
    checkOutDir = "build/checkout"
    explicitGormSrc = System.getProperty("gorm.src") ?: (project.hasProperty('gorm.src') ? project.getProperty("gorm.src") : null)
    gormSrc = explicitGormSrc ? file(explicitGormSrc).absolutePath : "$checkOutDir/gorm-src"
    zipFile = "build/source.zip"

    coreProjects = [
            'core',
            'gorm'
    ]

}

version rootProject.version

apply plugin: 'groovy'
apply plugin: 'org.asciidoctor.jvm.convert'

dependencies {
    documentation "org.grails:grails-core"
    documentation "org.grails:grails-bootstrap"
    documentation "org.grails:grails-spring"
    documentation "info.picocli:picocli:$picocliVersion"
    documentation "org.fusesource.jansi:jansi"
    documentation "org.apache.groovy:groovy-dateutil"
    documentation "com.github.javaparser:javaparser-core"

    documentation "org.fusesource.jansi:jansi"
    for(p in coreProjects) {
        documentation "org.grails:grails-datastore-$p"
    }
    project.rootProject.subprojects.each { subproject ->
        if(subproject.name != "docs" && !subproject.name.startsWith('examples')) {
            documentation project(":$subproject.name")
        }
    }
}

asciidoctor {
    inputs.dir layout.projectDirectory.dir('src/docs/asciidoc')
    outputs.dir layout.buildDirectory.dir('docs/manual')
    baseDirFollowsSourceDir()
    resources {
        from("${project.projectDir}/src/docs/asciidoc/images")
        into "./images"
    }

    attributes  'experimental'  : 'true',
                'compat-mode'   : 'true',
                'toc'           : 'left',
                'icons'         : 'font',
                'reproducible'  : '',
                'version'       : project.version,
                'pluginVersion' : project.version,
                'groupId'       : project.group,
                'artifactId'    : project.name,
                'sourcedir'     : "${project.projectDir}/src/main/groovy",
                'migrationPluginExamplesDir' : rootProject.layout.projectDirectory.dir('grails-database-migration/src/integration-test/resources').asFile.absolutePath,
                'migrationPluginGroupId' : rootProject.findProject(':database-migration').group,
                'migrationPluginArtifactId' : rootProject.findProject(':database-migration').name,
                'liquibaseHibernate5Version': liquibaseHibernate5Version
}

task fetchSource {
    outputs.dir layout.buildDirectory.dir('checkout')
    inputs.properties(branch: datastoreGithubBranch)

    onlyIf {
        println "GORM SRC=$explicitGormSrc"
        return !explicitGormSrc
    }

    doLast {
        println "Downloading GORM source code."
        if (isReleaseVersion) {
            ant.get src: "https://github.com/grails/grails-data-mapping/archive/refs/tags/v${gormVersion}.zip", dest: zipFile, verbose: true
        } else {
            ant.get src: "https://github.com/grails/grails-data-mapping/archive/refs/heads/${datastoreGithubBranch}.zip", dest: zipFile, verbose: true
        }

        ant.unzip src: zipFile, dest: checkOutDir, {
            mapper type: "regexp", from: "(grails-\\S*?/)(.*)", to: "gorm-src/\\2"
        }
        println "GORM source code downloaded."
    }
}

task copyDocs(type:Copy, dependsOn:asciidoctor) {
    mustRunAfter('asciidoctor', 'groovydoc', 'copyResources')
    finalizedBy('cleanAsciidoc')
    from project(':docs').layout.buildDirectory.dir('docs/asciidoc')
    into project(':docs').layout.buildDirectory.dir('docs/manual')
}

task cleanAsciidoc(type: Delete, dependsOn: copyDocs) {
    dependsOn('copyDocs')
    delete project(':docs').layout.buildDirectory.dir('docs/asciidoc')
}

tasks.withType(Groovydoc) {
    dependsOn('fetchSource')
    docTitle = "GORM for Hibernate 5 - ${project.version}"
    destinationDir = project.file('build/docs/api')

    def files
    for (p in coreProjects) {
        if (files == null) {
            files = project.files("${checkOutDir}/gorm-src/grails-datastore-${p}/src/main/groovy")
        } else {
            files += project.files("${checkOutDir}/gorm-src/grails-datastore-${p}/src/main/groovy")
        }
    }
    project.rootProject.subprojects
            .findAll { !it.name.contains('-rx-') && !it.name.startsWith('examples') }
            .each { subproject ->
                if (subproject.file('src/main/groovy').exists()) {
                    files += subproject.files("src/main/groovy")
                }
            }
    source = files
    classpath += configurations.documentation
}

task copyResources(type:Copy) {
    from 'src/docs/resources'
    into "${project.layout.buildDirectory.get()}/docs"
}

task docs(dependsOn:[asciidoctor, groovydoc, copyDocs, copyResources] +
        subprojects.findAll { project -> project.tasks.findByName('groovydoc')}
                .collect { project -> project.tasks.groovydoc }
) {
}

task assembleDocsDist(type: Zip) {
    dependsOn 'docs', 'copyDocs'
    from "${project.layout.buildDirectory.get()}/docs"
    include '*'
    include '*/**'
    archiveFileName = "${project.name}-${project.version}.zip"
    destinationDirectory = project.layout.buildDirectory.dir('distributions')
}

docs.finalizedBy assembleDocsDist
